plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = "net.andreinc"
version = "0.9.4"
archivesBaseName = "mapneat"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm-host"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.jayway.jsonpath:json-path:2.5.0"
    implementation "org.json:json:20201115"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.0-rc1"
    implementation "org.apache.logging.log4j:log4j-api-kotlin:1.0.0"
    implementation "org.apache.logging.log4j:log4j-api:2.14.0"
    implementation "org.apache.logging.log4j:log4j-core:2.14.0"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'mapneat'
                packaging 'jar'
                description "JSON to JSON and XML to JSON transformation library"
                url 'https://github.com/nomemory/mapneat'

                scm {
                    connection 'https://github.com/nomemory/mapneat'
                    developerConnection 'https://github.com/nomemory/mapneat'
                    url 'https://github.com/nomemory/mapneat'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'nomemory'
                        name 'nomemory'
                        email 'gnomemory@yahoo.com'
                    }
                }
            }
        }
    }
}